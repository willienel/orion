apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion versions.sdk.compile

    defaultConfig {

        applicationId "za.co.willienel.orion"

        minSdkVersion versions.sdk.min
        targetSdkVersion versions.sdk.target

        versionName versions.versionName()
        versionCode versions.versionCode()
    }

    signingConfigs {

        release {
            storeFile file(ORION_RELEASE_STORE_FILE)
            storePassword ORION_RELEASE_STORE_PASSWORD
            keyAlias ORION_RELEASE_KEY_ALIAS
            keyPassword ORION_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {

        debug {

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            applicationIdSuffix ".debug"
        }

        release {

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    sourceSets {

        main.java.srcDirs += 'src/main/kotlin'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def appName = "orion"
            outputFileName = appName + "-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':domain')
    implementation project(':data')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.tools.kotlin}"

    implementation "androidx.appcompat:appcompat:${versions.libraries.appCompat}"
    implementation "androidx.core:core-ktx:${versions.libraries.coreKtx}"
    implementation "androidx.constraintlayout:constraintlayout:${versions.libraries.constraintLayout}"

    kapt "androidx.lifecycle:lifecycle-compiler:${versions.libraries.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.libraries.lifecycle}"

    implementation "org.kodein.di:kodein-di-generic-jvm:${versions.libraries.kodein}"
    implementation "org.kodein.di:kodein-di-framework-android-x:${versions.libraries.kodein}"

    implementation "io.reactivex.rxjava2:rxjava:${versions.libraries.rxJava}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.libraries.rxAndroid}"

    implementation "com.jakewharton.timber:timber:${versions.libraries.timber}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:${versions.libraries.leakCanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${versions.libraries.leakCanary}"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:${versions.libraries.leakCanary}"
}
